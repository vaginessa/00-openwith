apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.android-command'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'
apply plugin: 'net.ltgt.errorprone'

def signingFile = file('props/release.signing')
buildProperties {
    signing.file signingFile.exists() ? signingFile : file('props/debug.signing')
    secrets.file file('props/secrets.properties')
}

def secrets = buildProperties.secrets
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'com.tasomaniac.openwith'
        minSdkVersion 19
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha}\""

        resConfigs 'ar', 'de', 'en', 'es', 'fr', 'in', 'it', 'ja', 'pl', 'pt-rBR', 'pt-rPT', 'ru', 'tr', 'uk', 'zh-rCN'

        javaCompileOptions {
            annotationProcessorOptions {
                argument('butterknife.minSdk', minSdkVersion.apiString)
            }
        }
    }

    signingConfigs {
        debug.storeFile file('props/debug.keystore')
        release.signingConfigProperties buildProperties.signing
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            versionNameSuffix '-debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'store'
    productFlavors {
        floss {
            applicationIdSuffix '.floss'
            versionNameSuffix '-floss'
        }
        play {}
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        lintConfig file('props/lint.xml')
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
        checkReleaseBuilds false
    }
}

play {
    serviceAccountEmail = secrets['play_service_account'].or('').string
    pk12File = file(secrets['play_key_file'].or('no_file.p12').string)
    track = 'beta'
}

configurations {
    implementation.exclude module: 'support-v4'
    compile.exclude module: 'support-v4'
}

dependencies {
    def supportLibVersion = '26.0.0'
    def schematicVersion = '0.7.0'
    def butterKnifeVersion = '8.7.0'
    def daggerVersion = '2.11'
    def okHttpVersion = '3.8.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation 'com.duolingo.open:rtl-viewpager:1.0.2'

    implementation 'com.tasomaniac:delayed-progress:0.4'

    implementation "net.simonvt.schematic:schematic:$schematicVersion"
    kapt "net.simonvt.schematic:schematic-compiler:$schematicVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    implementation 'com.jakewharton.timber:timber:4.5.1'

    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.2'

    playImplementation 'com.crashlytics.sdk.android:crashlytics:2.6.8'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
}
